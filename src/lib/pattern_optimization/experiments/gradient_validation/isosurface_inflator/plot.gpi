# usage: gnuplot -e "nsvType='direct'; deg=1; paramNum=0;" plot.gpi
set terminal pngcairo size 1024,1024
set output sprintf("results/%s_nsv/d%i/plot_%i.png", nsvType, deg, paramNum);

set multiplot layout 2, 1 title sprintf("Shape Velocity %i Validation (%s normal shape velocity, deg %i)", paramNum, nsvType, deg) noenhanced
dataSource=sprintf("< ./data.py results/%s_nsv/d%i/p%i.txt", nsvType, deg, paramNum);

set grid
set xlabel sprintf("param %i", paramNum)

set title "J_S"
plot dataSource u 1:2 with lp notitle

set title sprintf("dJ_S/dp%i", paramNum)
plot dataSource u 1:3 with lp title "Shape derivative", \
     dataSource u 1:4 with l  title "Centered difference", \
     dataSource u 1:5 with l  title "Forward difference"
