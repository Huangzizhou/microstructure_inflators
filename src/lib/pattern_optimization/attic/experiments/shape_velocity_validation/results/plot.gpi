# usage: gnuplot -e "prefix='direct_nsv_'; paramNum=0" plot.gpi
set terminal pngcairo size 1024,1024
set output sprintf("plot_%s%i.png", prefix, paramNum)

set multiplot layout 2, 1 title sprintf("Shape Velocity %i Validation (%s)", paramNum, prefix) noenhanced
dataSource=sprintf("< ./data.py %s%i.txt", prefix, paramNum);

set grid
set xlabel sprintf("param %i", paramNum)

set title "Volume"
plot dataSource u 1:2 with lp notitle

set title sprintf("dVolume/dp%i", paramNum)
plot dataSource u 1:3 with lp title "Shape derivative", \
     dataSource u 1:4 with l  title "Centered difference", \
     dataSource u 1:5 with l  title "Forward difference"
