# usage: gnuplot -e "paramName='radius';" lphole_plot_wc.gpi
set terminal pngcairo size 1024,1024
set output sprintf("LpHole/wc_plot_%s.png", paramName);

set multiplot layout 3, 1 title sprintf("Worst Case Stress %s Gradient Component Validation", paramName) noenhanced
dataSource=sprintf("< ./data.py LpHole/%s.txt", paramName);

set grid
set xlabel sprintf("%s", paramName)

set title "WCS"
plot dataSource u 1:6 with lp notitle

set title sprintf("dWCS/d%s", paramName)
plot dataSource u 1: 7 with lp title "Direct derivative", \
     ""         u 1: 8 with lp title "Adjoint method derivative", \
     ""         u 1: 9 with l  title "Centered difference", \
     ""         u 1:10 with l  title "Forward difference"

set title sprintf("Direct dWCS/d%s", paramName)
plot dataSource u 1: 9 with l  title "Centered Difference", \
     ""         u 1:11 with l  title "Fluctuation Change Term", \
     ""         u 1:12 with l  title "Boundary Advection Term", \
     ""         u 1: 7 with l  title "Total Direct derivative"
