# usage: gnuplot -e "deg=1; area='0.00001'; radius='0.25'" lphole_plot_Js.gpi
set terminal pngcairo size 1024,746
name=sprintf('d%i_a%s_r%s', deg, area, radius);

dataSource=sprintf("< ./lphole_wcstress_min_data.py %i %s %s", deg, area, radius)

set grid
set xlabel "Lp Objective p value"

set yrange [0.4:1.8]

set output sprintf("%s.minimizer.png", name);

set title sprintf("Optimal Hole Parameter (Deg %i, area %s, radius %s)", deg, area, radius)
set ylabel "Optimal Hole Parameter (p)"
plot dataSource u 1:2 with lp notitle

set autoscale y

set title sprintf("WCS At Optimal Hole Parameter (Deg %i, area %s, radius %s)", deg, area, radius)
set ylabel "Worst Case Stress Frobenius Norm Field Lp Norm"
set output sprintf("%s.minimum.png", name);

plot dataSource u 1:3 with lp notitle
