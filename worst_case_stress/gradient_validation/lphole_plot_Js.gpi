# usage: gnuplot -e "paramName='radius';" lphole_plot_Js.gpi
set terminal pngcairo size 1024,1024
set output sprintf("LpHole/js_plot_%s.png", paramName);

set multiplot layout 2, 1 title sprintf("%s Shape Velocity Validation", paramName) noenhanced
dataSource=sprintf("< ./data.py LpHole/%s.txt", paramName);

set grid
set xlabel sprintf("%s", paramName)

set title "J_S"
plot dataSource u 1:2 with lp notitle

set title sprintf("dJ_S/d%s", paramName)
plot dataSource u 1:3 with lp title "Shape derivative", \
     dataSource u 1:4 with l  title "Centered difference", \
     dataSource u 1:5 with l  title "Forward difference"
