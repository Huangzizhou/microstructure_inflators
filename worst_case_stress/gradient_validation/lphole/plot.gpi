# usage: gnuplot -e "resultDir='results'; P=1; paramNum=0; pCenter='1.1'; rangeDir='ranges'" plot.gpi
set terminal pngcairo size 1024,1024

quantities="JFull WCS JS";
do for [i=1:words(quantities)] {
quantity = word(quantities, i);
set output sprintf("%s/P%i_param%i.%s.png", resultDir, P, paramNum, quantity);
set multiplot layout 2, 1 title sprintf("%s Gradient Validation Param %i", quantity, paramNum) noenhanced
dataSource=sprintf("< ./data.py %s/P%i_param%i.txt", resultDir, P, paramNum);

set grid
set xlabel sprintf("param %i", paramNum)

set title quantity
if (exists("rangeDir")) {
    eval(sprintf('rangeCmd = "\`cat %s/pc%s_P%i_param%i.%s.range\`";', rangeDir, pCenter, P, paramNum, quantity));
    eval(rangeCmd);
}

plot dataSource u "PARAM":quantity with lp notitle

set title sprintf("d%s/dp%i", quantity, paramNum)
if (exists("rangeDir")) {
    eval(sprintf('rangeCmd = "\`cat %s/pc%s_P%i_param%i.D%s.range\`";', rangeDir, pCenter, P, paramNum, quantity));
    eval(rangeCmd);
}

if (i == 2) {
    plot dataSource u "PARAM":sprintf("SD_%s", quantity) with lp title "Shape derivative (Adjoint)", \
         ''         u "PARAM":sprintf("SD_%s_DIRECT", quantity) with lp title "Shape derivative (Direct)", \
         ''         u "PARAM":sprintf("CD_%s", quantity) with l  title "Centered difference", \
         ''         u "PARAM":sprintf("FD_%s", quantity) with l  title "Forward difference"
} else {
    plot dataSource u "PARAM":sprintf("SD_%s", quantity) with lp title "Shape derivative (Adjoint)", \
         ''         u "PARAM":sprintf("CD_%s", quantity) with l  title "Centered difference", \
         ''         u "PARAM":sprintf("FD_%s", quantity) with l  title "Forward difference"
}

unset multiplot

}
